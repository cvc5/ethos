; This file defines the semantics of Eunoia operators that require calling
; SMT-LIB like computations.

; Embedding into SMT operators, which defines $smt_builtin_*, $smt_apply_* etc.
(include "smt_embed.eo")

; In this signature, we heavily use references to the Datatypes corresponding
; to the final deep embeddings of Eunoia terms (eo.Term).
; Opaque arguments of a symbol X will be an argument of the constructor eo.X.
; We introduce datatype constructors for this datatype ($emb_X).
; It is important that we mark their arguments as :opaque, as this
; correctly models the pattern matching in the smt-meta later.
; Note that these datatypes will be renamed and replaced by their definitions
; which is hardcoded into smt_meta.smt2.

;;; Final deep embedding datatypes

; Now, we introduce placeholders for the final deep embedding.

; A Eunoia term.
; This will be replaced by the datatype eo.Term in the final encoding.
(declare-const $eo_Term Type)

; Assumes that teq is defined to be equality over eo.Term
(define $smt_builtin_teq ((x1 $eo_Term) (x2 $eo_Term)) ($smt_apply_2 "teq" x1 x2))

;;; Eunoia datatype constructors

; The Boolean type
(declare-const $emb_Bool $eo_Term)
(define $eo_Bool () $emb_Bool)

; the literal types
(declare-parameterized-const $emb_Boolean
  ((b $smt_builtin_Bool :opaque)) $eo_Term)
(define $eo_bool ((x $smt_builtin_Bool))
  ($emb_Boolean x))

(declare-parameterized-const $emb_Numeral
  ((n $smt_builtin_Int :opaque)) $eo_Term)
(define $eo_numeral ((x $smt_builtin_Int))
  ($emb_Numeral x))

(declare-parameterized-const $emb_Rational
  ((r $smt_builtin_Rat :opaque)) $eo_Term)
(define $eo_rational ((x $smt_builtin_Rat))
  ($emb_Rational x))

(declare-parameterized-const $emb_String
  ((s $smt_builtin_String :opaque)) $eo_Term)
(define $eo_string ((x $smt_builtin_String))
  ($emb_String x))

(declare-parameterized-const $emb_Binary
  ((w $smt_builtin_Int :opaque) (v $smt_builtin_Int :opaque)) $eo_Term)
; define: $eo_binary
; note: >
;   This should be used in pattern matching only, where it is assume that
;   it will match "legal" binary values only.
(define $eo_binary ((w $smt_builtin_Int) (v $smt_builtin_Int))
  ($emb_Binary w v))

; The type of types
(declare-const $emb_Type $eo_Term)
(define $eo_Type () $emb_Type)

; The Eunoia representation of "stuckness".
(declare-const $emb_Stuck $eo_Term)
(define $eo_stuck () $emb_Stuck)

; An application of a Eunoia term
(declare-parameterized-const $emb_Apply
  ((f $eo_Term :opaque) (a $eo_Term :opaque))
  $eo_Term)
(define $eo_apply ((x $eo_Term) (y $eo_Term)) ($emb_Apply x y))

; Function type, which Eunoia function type reduces to
(declare-parameterized-const $emb_FunType () $eo_Term)
(define $eo_fun_type () $emb_FunType)

; Represents a Eunoia variable in the final deep embedding.
(declare-parameterized-const $emb_Var
  ((s $smt_builtin_String :opaque) (T $eo_Term :opaque))
  $eo_Term)
(define $eo_Var ((s $smt_builtin_String) (T $eo_Term))
  ($emb_Var s T))

;;; Utilities

; return: The embedding of (-> T1 T2).
; note: Since the Eunoia function type is an atomic term, this becomes a
;       curried apply.
(define $eo_mk_fun_type ((T1 $eo_Term) (T2 $eo_Term))
  ($eo_apply ($eo_apply $eo_fun_type T1) T2))

; note: This definition is only used if we are not eagerly flattening evaluation
; This ensures that stuckness "propagates" through ordinary constant applications.
(program $eo_mk_apply ((x1 $eo_Term) (x2 $eo_Term))
  :signature ($eo_Term $eo_Term) $eo_Term
  (
  (($eo_mk_apply x1 x2) ($eo_apply x1 x2))
  )
)

;;; Defining parametric bitvector operators

; The operators in this section are needed to reason about the parametric
; operations on the Eunoia binary literal type.

; define: $eo_empty_binary
(define $eo_empty_binary ()
  ($eo_binary $smt_builtin_z_zero $smt_builtin_z_zero))

; note: Assumes 0 <= w, makes n legal. We cannot handle errors with
;       w here since we cannot return stuckness.
(define $eo_binary_mod_w ((w $smt_builtin_Int) (n $smt_builtin_Int))
  ($eo_binary w ($smt_builtin_mod n ($smtx_pow2 w))))

; program: $eo_mk_binary
; return: >
;   A "legal" binary value ($eo_mk_binary w n1), where 0 <= n1 < 2^w, n1 is
;   congruent mod 2^w to n if w is positive, or stuck otherwise.
; note: >
;   All binaries considered by Eunoia should run through this method to ensure
;   they are legal. We alternatively use ($eo_binary_mod_w w n) if
;   we are sure that w is legal but not n.
(program $eo_mk_binary ((w $smt_builtin_Int) (n $smt_builtin_Int))
  :signature ($smt_builtin_Int $smt_builtin_Int) $eo_Term
  (
  (($eo_mk_binary w n)
    ($smt_builtin_ite ($smt_builtin_z_<= $smt_builtin_z_zero w)
      ($eo_binary_mod_w w n)  ; ensure that n is legal
      $eo_stuck))
  )
)

;;;;;; Eunoia builtin

(program $eo_typeof () :signature ($eo_Term) $eo_Term)

; We are now ready to define the builtins of Eunoia.
; We define all the Eunoia builtins here that are best to
; define in terms of SMT-LIB operators.

;;; Is okay

; Returns SMT-LIB builtin true/false if x is stuck
(define $smt_builtin_is_ok ((x $eo_Term))
  ($smt_builtin_not ($smt_builtin_teq x $eo_stuck)))

; program: $eo_is_ok
; implements: eo::is_ok
(define $eo_is_ok ((x $eo_Term))
  ($eo_bool ($smt_builtin_is_ok x)))

; program: $eo_ite
; implements: eo::ite
; note: This definition is only used if we are not eagerly flattening evaluation
(define $eo_ite ((x1 $eo_Term) (x2 $eo_Term) (x3 $eo_Term))
  ($smt_builtin_ite ($smt_builtin_teq x1 true)
    x2
  ($smt_builtin_ite ($smt_builtin_teq x1 false)
    x3
    $eo_stuck)))

; program: $eo_requires
; implements: eo::requires
; note: This definition is only used if we are not eagerly flattening evaluation
(define $eo_requires ((x1 $eo_Term) (x2 $eo_Term) (x3 $eo_Term))
  ($smt_builtin_ite ($smt_builtin_teq x1 x2)
    ($smt_builtin_ite ($smt_builtin_is_ok x1) x3 $eo_stuck)
    $eo_stuck))

;;; Boolean operators

; program: $eo_not
; implements: eo::not
(program $eo_not ((T Type) (b $smt_builtin_Bool) (w $smt_builtin_Int) (n $smt_builtin_Int))
  :signature (T) T
  (
  (($eo_not ($eo_bool b))     ($eo_bool ($smt_builtin_not b)))
  (($eo_not ($eo_binary w n)) ($eo_binary_mod_w w ($smtx_binary_not w n)))
  )
)

; program: $eo_and
; implements: eo::and
(program $eo_and
  ((T Type) (b1 $smt_builtin_Bool) (b2 $smt_builtin_Bool)
   (w1 $smt_builtin_Int) (w2 $smt_builtin_Int)
   (n1 $smt_builtin_Int) (n2 $smt_builtin_Int))
  :signature (T T) T
  (
  (($eo_and ($eo_bool b1) ($eo_bool b2))
     ($eo_bool ($smt_builtin_and b1 b2)))
  (($eo_and ($eo_binary w1 n1) ($eo_binary w2 n2))
    ($eo_requires ($eo_numeral w1) ($eo_numeral w2)
      ($eo_binary_mod_w w1 ($smtx_binary_and w1 n1 n2))))
  )
)

; program: $eo_or
; implements: eo::or
(program $eo_or
  ((T Type) (b1 $smt_builtin_Bool) (b2 $smt_builtin_Bool)
   (w1 $smt_builtin_Int) (w2 $smt_builtin_Int)
   (n1 $smt_builtin_Int) (n2 $smt_builtin_Int))
  :signature (T T) T
  (
  (($eo_or ($eo_bool b1) ($eo_bool b2))
     ($eo_bool ($smt_builtin_or b1 b2)))
  (($eo_or ($eo_binary w1 n1) ($eo_binary w2 n2))
    ($eo_requires ($eo_numeral w1) ($eo_numeral w2)
     ($eo_binary_mod_w w1 ($smtx_binary_or w1 n1 n2))))
  )
)

; program: $eo_xor
; implements: eo::xor
(program $eo_xor
  ((T Type) (b1 $smt_builtin_Bool) (b2 $smt_builtin_Bool)
   (w1 $smt_builtin_Int) (w2 $smt_builtin_Int)
   (n1 $smt_builtin_Int) (n2 $smt_builtin_Int))
  :signature (T T) T
  (
  (($eo_xor ($eo_bool b1) ($eo_bool b2))
     ($eo_bool ($smt_apply_2 "xor" b1 b2)))
  (($eo_xor ($eo_binary w1 n1) ($eo_binary w2 n2))
    ($eo_requires ($eo_numeral w1) ($eo_numeral w2)
     ($eo_binary_mod_w w1 ($smtx_binary_or w1 n1 n2))))
  )
)

;;; Arithmetic operators

; program: $eo_add
; implements: eo::add
(program $eo_add
  ((T Type) (w1 $smt_builtin_Int) (w2 $smt_builtin_Int)
   (n1 $smt_builtin_Int) (n2 $smt_builtin_Int) (r1 $smt_builtin_Rat) (r2 $smt_builtin_Rat))
  :signature (T T) T
  (
  (($eo_add ($eo_numeral n1) ($eo_numeral n2))
    ($eo_numeral ($smt_builtin_z_+ n1 n2)))
  (($eo_add ($eo_rational r1) ($eo_rational r2))
    ($eo_rational ($smt_builtin_q_+ r1 r2)))
  (($eo_add ($eo_binary w1 n1) ($eo_binary w2 n2))
    ($eo_requires ($eo_numeral w1) ($eo_numeral w2)
      ($eo_binary_mod_w w1 ($smt_builtin_z_+ n1 n2))))  ; don't need to recheck bitwidth
  )
)

(program $eo_mul
  ((T Type) (w1 $smt_builtin_Int) (w2 $smt_builtin_Int)
   (n1 $smt_builtin_Int) (n2 $smt_builtin_Int) (r1 $smt_builtin_Rat) (r2 $smt_builtin_Rat))
  :signature (T T) T
  (
  (($eo_mul ($eo_numeral n1) ($eo_numeral n2))
    ($eo_numeral ($smt_builtin_z_* n1 n2)))
  (($eo_mul ($eo_rational r1) ($eo_rational r2))
    ($eo_rational ($smt_builtin_q_* r1 r2)))
  (($eo_mul ($eo_binary w1 n1) ($eo_binary w2 n2))
    ($eo_requires ($eo_numeral w1) ($eo_numeral w2)
      ($eo_binary_mod_w w1 ($smt_builtin_z_* n1 n2))))
  )
)

(program $eo_qdiv
  ((T Type) (n1 $smt_builtin_Int) (n2 $smt_builtin_Int)
   (r1 $smt_builtin_Rat) (r2 $smt_builtin_Rat))
  :signature (T T) $eo_Term
  (
  (($eo_qdiv ($eo_numeral n1) ($eo_numeral n2))
    (eo::define ((rn1 ($smt_apply_1 "to_real" n1)))
    (eo::define ((rn2 ($smt_apply_1 "to_real" n2)))
    ($smt_builtin_ite ($smt_builtin_z_= $smt_builtin_z_zero n2)
      $eo_stuck
      ($eo_rational ($smt_apply_2 "/" rn1 rn2))))))
  (($eo_qdiv ($eo_rational r1) ($eo_rational r2))
    ($smt_builtin_ite ($smt_builtin_q_= $smt_builtin_q_zero r2)
      $eo_stuck
      ($eo_rational ($smt_apply_2 "/" r1 r2))))
  )
)


(program $eo_zdiv
  ((T Type) (w1 $smt_builtin_Int) (w2 $smt_builtin_Int)
   (n1 $smt_builtin_Int) (n2 $smt_builtin_Int))
  :signature (T T) T
  (
  (($eo_zdiv ($eo_numeral n1) ($eo_numeral n2))
    ($smt_builtin_ite ($smt_builtin_z_= $smt_builtin_z_zero n2)
      $eo_stuck
      ($eo_numeral ($smt_builtin_div n1 n2))))
  (($eo_zdiv ($eo_binary w1 n1) ($eo_binary w2 n2))
    ($eo_requires ($eo_numeral w1) ($eo_numeral w2)
      ($smt_builtin_ite ($smt_builtin_z_= $smt_builtin_z_zero n2)
        ($eo_binary w1 ($smtx_binary_max w1))
        ($eo_binary_mod_w w1 ($smt_builtin_div n1 n2)))))
  )
)

(program $eo_zmod
  ((T Type) (w1 $smt_builtin_Int) (w2 $smt_builtin_Int)
   (n1 $smt_builtin_Int) (n2 $smt_builtin_Int))
  :signature (T T) T
  (
  (($eo_zmod ($eo_numeral n1) ($eo_numeral n2))
    ($smt_builtin_ite ($smt_builtin_z_= $smt_builtin_z_zero n2)
      $eo_stuck
      ($eo_numeral ($smt_builtin_mod n1 n2))))
  (($eo_zmod ($eo_binary w1 n1) ($eo_binary w2 n2))
    ($eo_requires ($eo_numeral w1) ($eo_numeral w2)
      ($smt_builtin_ite ($smt_builtin_z_= $smt_builtin_z_zero n2)
        ($eo_binary w1 n1)
        ($eo_binary_mod_w w1 ($smt_builtin_mod n1 n2)))))
  )
)

(program $eo_is_neg ((T Type) (n1 $smt_builtin_Int) (r1 $smt_builtin_Rat))
  :signature (T) Bool
  (
  (($eo_is_neg ($eo_numeral n1))
    ($eo_bool ($smt_builtin_z_< n1 $smt_builtin_z_zero)))
  (($eo_is_neg ($eo_rational r1))
    ($eo_bool ($smt_builtin_q_< r1 $smt_builtin_q_zero)))
  )
)

; program: $eo_neg
; implements: eo::neg
(program $eo_neg ((T Type) (w $smt_builtin_Int) (n1 $smt_builtin_Int) (r1 $smt_builtin_Rat))
  :signature (T) T
  (
  (($eo_neg ($eo_numeral n1))
    ($eo_numeral ($smt_builtin_z_neg n1)))
  (($eo_neg ($eo_rational r1))
    ($eo_rational ($smt_builtin_q_neg r1)))
  (($eo_neg ($eo_binary w n1))
    ($eo_binary_mod_w w ($smt_builtin_z_neg n1)))
  )
)


;;; String operators

; program: $eo_len
; implements: eo::len
(program $eo_len ((T Type) (s1 $smt_builtin_String) (w $smt_builtin_Int) (n1 $smt_builtin_Int))
  :signature (T) $eo_Term
  (
  (($eo_len ($eo_string s1))
    ($eo_numeral ($smt_apply_1 "str.len" s1)))
  (($eo_len ($eo_binary w n1))
    ($eo_numeral w))
  )
)

; program: $eo_concat
; implements: eo::concat
(program $eo_concat
  ((w1 $smt_builtin_Int) (n1 $smt_builtin_Int) (w2 $smt_builtin_Int) (n2 $smt_builtin_Int)
   (s1 $smt_builtin_String) (s2 $smt_builtin_String))
  :signature ($eo_Term $eo_Term) $eo_Term
  (
  (($eo_concat ($eo_string s1) ($eo_string s2))
     ($eo_string ($smt_apply_2 "str.++" s1 s2)))
  (($eo_concat ($eo_binary w1 n1) ($eo_binary w2 n2))
     ($eo_mk_binary ($smt_builtin_z_+ w1 w2) ($smtx_binary_concat w1 n1 w2 n2)))
  )
)

; program: $eo_extract
; implements: eo::extract
(program $eo_extract
  ((s1 $smt_builtin_String) (w $smt_builtin_Int) (n1 $smt_builtin_Int) (n2 $smt_builtin_Int) (n3 $smt_builtin_Int))
  :signature ($eo_Term $eo_Term $eo_Term) $eo_Term
  (
  (($eo_extract ($eo_string s1) ($eo_numeral n2) ($eo_numeral n3))
     ($eo_string ($smt_apply_3 "str.substr" s1 n2 ($smt_builtin_z_inc ($smt_builtin_z_- n3 n2)))))
  (($eo_extract ($eo_binary w n1) ($eo_numeral n2) ($eo_numeral n3))
    (eo::define ((nw ($smt_builtin_z_- n3 n2)))
    ($smt_builtin_ite ($smt_builtin_or ($smt_builtin_z_< n2 $smt_builtin_z_zero) ($smt_builtin_z_< nw $smt_builtin_z_zero))
      $eo_empty_binary
      ($eo_mk_binary
        ($smt_builtin_z_inc nw)
        ($smtx_binary_extract w n1 n2 n3)))))
  )
)

; program: $eo_find
; implements: eo::find
(program $eo_find ((s1 $smt_builtin_String) (s2 $smt_builtin_String))
  :signature ($eo_Term $eo_Term) $eo_Term
  (
  (($eo_find ($eo_string s1) ($eo_string s2))
     ($eo_numeral ($smt_apply_3 "str.indexof" s1 s2 $smt_builtin_z_zero)))
  )
)

;;; Conversion operators

; program: $eo_to_z
; implements: eo::to_z
(program $eo_to_z ((n1 $smt_builtin_Int) (r1 $smt_builtin_Rat) (s1 $smt_builtin_String) (w $smt_builtin_Int))
  :signature ($eo_Term) $eo_Term
  (
  (($eo_to_z ($eo_numeral n1))   ($eo_numeral n1))
  (($eo_to_z ($eo_rational r1))  ($eo_numeral ($smt_apply_1 "to_int" r1)))
  (($eo_to_z ($eo_string s1))
    ($smt_builtin_ite
      ($smt_builtin_z_= $smt_builtin_z_one ($smt_apply_1 "str.len" s1))
      ($eo_numeral ($smt_apply_1 "str.to_code" s1))
      $eo_stuck))
  (($eo_to_z ($eo_binary w n1))     ($eo_numeral n1))
  )
)

; program: $eo_to_q
; implements: eo::to_q
(program $eo_to_q ((n1 $smt_builtin_Int) (r1 $smt_builtin_Rat) (s1 $smt_builtin_String) (w $smt_builtin_Int))
  :signature ($eo_Term) $eo_Term
  (
  (($eo_to_q ($eo_numeral n1))   ($eo_rational ($smt_apply_1 "to_real" n1)))
  (($eo_to_q ($eo_rational r1))  ($eo_rational r1))
  )
)

; program: $eo_to_bin
; implements: eo::to_bin
(program $eo_to_bin ((w $smt_builtin_Int) (w1 $smt_builtin_Int) (n1 $smt_builtin_Int))
  :signature ($eo_Term $eo_Term) $eo_Term
  (
  (($eo_to_bin ($eo_numeral w) ($eo_numeral n1))
    ; the upper bound is enforced here
    ($smt_builtin_ite ($smt_builtin_z_<= w $smt_builtin_z_max_width)
      ($eo_mk_binary w n1)
      $eo_stuck))
  (($eo_to_bin ($eo_numeral w) ($eo_binary w1 n1))
    ; the upper bound is enforced here
    ($smt_builtin_ite ($smt_builtin_z_<= w $smt_builtin_z_max_width)
      ($eo_mk_binary w n1)
      $eo_stuck))
  )
)

; program: $eo_to_str
; implements: eo::to_str
(program $eo_to_str ((n1 $smt_builtin_Int) (s1 $smt_builtin_String))
  :signature ($eo_Term) $eo_Term
  (
  (($eo_to_str ($eo_numeral n1))
    ($smt_builtin_ite
      ($smt_builtin_and ($smt_builtin_z_<= $smt_builtin_z_zero n1) ($smt_builtin_z_< n1 $smt_builtin_z_max_code))
      ($eo_string ($smt_apply_1 "str.from_code" n1))
      $eo_stuck))
  (($eo_to_str ($eo_string s1)) ($eo_string s1))
  ; TODO: technically others here
  )
)

;;; Testers

;;; $eo_is_eq

;;; $eo_eq

; program: $eo_eq
; implements: eo::eq
(program $eo_eq ((t $eo_Term) (s $eo_Term))
  :signature ($eo_Term $eo_Term) $eo_Term
  (
  (($eo_eq t s) ($eo_bool ($smt_builtin_teq s t)))
  )
)

; Returns true if t and s are equivalent values (ground and fully evaluated).
; define: $eo_is_eq
; implements: eo::is_eq
(define $eo_is_eq ((t $eo_Term) (s $eo_Term))
  ($eo_bool ($smt_apply_nary_3 "and"
    ($smt_builtin_is_ok t)
    ($smt_builtin_is_ok s)
    ($smt_builtin_teq s t))))

;;; $eo_is_bool

; Returns true if x is a Boolean literal.
(program $eo_is_bool_internal ((x $eo_Term) (b $smt_builtin_Bool))
  :signature ($eo_Term) Bool
  (
  (($eo_is_bool_internal ($eo_bool b))  true)
  (($eo_is_bool_internal x)                false)
  )
)

; Returns true if x is a Boolean literal.
; define: $eo_is_bool
; implements: eo::is_bool
(define $eo_is_bool ((t $eo_Term))
  ($eo_bool ($smt_builtin_and ($smt_builtin_is_ok t)
    ($smt_builtin_teq ($eo_is_bool_internal t) true))))

;;; $eo_is_z

; Returns true if x is a numeral literal.
(program $eo_is_z_internal ((x $eo_Term) (n $smt_builtin_Int))
  :signature ($eo_Term) Bool
  (
  (($eo_is_z_internal ($eo_numeral n))  true)
  (($eo_is_z_internal x)                false)
  )
)

; Returns true if x is a numeral literal.
; define: $eo_is_z
; implements: eo::is_z
(define $eo_is_z ((t $eo_Term))
  ($eo_bool ($smt_builtin_and ($smt_builtin_is_ok t)
    ($smt_builtin_teq ($eo_is_z_internal t) true))))

;;; $eo_is_q

; Returns true if x is a rational literal.
(program $eo_is_q_internal ((x $eo_Term) (r $smt_builtin_Rat))
  :signature ($eo_Term) Bool
  (
  (($eo_is_q_internal ($eo_rational r))  true)
  (($eo_is_q_internal x)                 false)
  )
)

; Returns true if x is a rational literal.
; define: $eo_is_q
; implements: eo::is_q
(define $eo_is_q ((t $eo_Term))
  ($eo_bool ($smt_builtin_and ($smt_builtin_is_ok t)
    ($smt_builtin_teq ($eo_is_q_internal t) true))))


;;; $eo_is_bin

; Returns true if x is a binary literal.
(program $eo_is_bin_internal ((x $eo_Term) (w $smt_builtin_Int) (n $smt_builtin_Int))
  :signature ($eo_Term) Bool
  (
  (($eo_is_bin_internal ($eo_binary w n))  true)
  (($eo_is_bin_internal x)                 false)
  )
)

; Returns true if x is a binary literal.
; define: $eo_is_bin
; implements: eo::is_bin
(define $eo_is_bin ((t $eo_Term))
  ($eo_bool ($smt_builtin_and ($smt_builtin_is_ok t)
    ($smt_builtin_teq ($eo_is_bin_internal t) true))))

;;; $eo_is_str

; Returns true if x is a string literal.
(program $eo_is_str_internal ((x $eo_Term) (s $smt_builtin_String))
  :signature ($eo_Term) Bool
  (
  (($eo_is_str_internal ($eo_string s)) true)
  (($eo_is_str_internal x)                 false)
  )
)

; Returns true if x is a string literal.
; define: $eo_is_str
; implements: eo::is_str
(define $eo_is_str ((t $eo_Term))
  ($eo_bool ($smt_builtin_and ($smt_builtin_is_ok t)
    ($smt_builtin_teq ($eo_is_str_internal t) true))))

;;; Derived arithmetic

; final-decl: $smtx_hash
; note: this is defined axiomatically in the final smt2 encoding.
(program $smtx_hash ((T Type)) :signature (T) $smt_builtin_Int)

; program: $eo_hash
; implements: eo::hash
; note: this invokes the underconstrained function above as an oracle.
(program $eo_hash ((T Type) (t T))
  :signature (T) $eo_Term
  (
  (($eo_hash t) ($eo_numeral ($smtx_hash t)))
  )
)

; final-decl: $eo_reverse_hash
; note: this is the inverse of $smtx_hash, which will be defined axiomatically.
(program $eo_reverse_hash () :signature ($smt_builtin_Int) $eo_Term)

; The SMT-LIB comparison of two Eunoia or SMT-LIB terms.
(define $smtx_cmp ((T Type :implicit) (a T) (b T))
  ($smt_builtin_z_<= ($smtx_hash a) ($smtx_hash b)))


;;; $eo_gt

; Compare arithmetic greater than. Assumes x and y are values.
; Returns true if x > y.
; define: $eo_gt
; implements: eo::gt
(define $eo_gt ((x $eo_Term) (y $eo_Term))
  ($eo_is_neg ($eo_add ($eo_neg x) y)))


;;; $eo_cmp

; An arbitrary deterministic comparison of terms. Returns true if a > b based
; on this ordering.
; define: $eo_cmp
; implements: eo::cmp
(define $eo_cmp ((a $eo_Term) (b $eo_Term))
  ($eo_is_neg ($eo_add ($eo_hash b) ($eo_neg ($eo_hash a)))))

;;; since eo_typeof depends on literal predicates, typeof is only now ready.

; program: $eo_var
; implements: eo::var
(program $eo_var ((s $smt_builtin_String) (T $eo_Term))
  :signature ($eo_Term $eo_Term) $eo_Term
  (
  (($eo_var ($eo_string s) T)
    ; we use the hash of the type T to identify the variable
    ($eo_requires ($eo_typeof T) Type ($eo_Var s T)))
  )
)

; program: $eo_nameof
; implements: eo::nameof
(program $eo_nameof ((s $smt_builtin_String) (T $eo_Term))
  :signature ($eo_Term) $eo_Term
  (
  (($eo_nameof ($eo_Var s T)) ($eo_string s))
  )
)

; Returns true if x is a variable.
; define: $eo_is_var
; implements: eo::is_var
(define $eo_is_var ((x $eo_Term))
  ($eo_is_eq ($eo_var ($eo_nameof x) ($eo_typeof x)) x))

; fwd-decl: $eo_typeof_main
; note: This method returns the type for all user defined terms.
(program $eo_typeof_main () :signature ($eo_Term) $eo_Term)

; forward declarations for literal type rules
(program $eo_lit_type_Numeral ((T Type)) :signature (T) Type)
(program $eo_lit_type_Rational ((T Type)) :signature (T) Type)
(program $eo_lit_type_Binary ((T Type)) :signature (T) Type)
(program $eo_lit_type_String ((T Type)) :signature (T) Type)
  
; program: $eo_typeof
; implements: eo::typeof
(program $eo_typeof
  ((t $eo_Term) (T $eo_Term) (b $smt_builtin_Bool) (n $smt_builtin_Int) (r $smt_builtin_Rat)
   (s $smt_builtin_String) (w $smt_builtin_Int))
  :signature ($eo_Term) $eo_Term
  (
  (($eo_typeof ($eo_bool b))                 Bool)
  (($eo_typeof ($eo_numeral n))              ($eo_lit_type_Numeral ($eo_numeral n)))
  (($eo_typeof ($eo_rational r))             ($eo_lit_type_Rational ($eo_rational r)))
  (($eo_typeof ($eo_string s))               ($eo_lit_type_String ($eo_string s)))
  (($eo_typeof ($eo_binary w n))             ($eo_lit_type_Binary ($eo_binary w n)))
  (($eo_typeof ($eo_Var s T))                T)
  ; otherwise, invoke the routine for user types from the signature.
  (($eo_typeof t)                            ($eo_typeof_main t))
  )
)

; fwd-decl: $eo_dt_constructors_main
; note: This method returns the constructors for all user defined terms.
(program $eo_dt_constructors_main () :signature ($eo_Term) $eo_Term)

; program: $eo_dt_constructors
; implements: eo::dt_constructors
(program $eo_dt_constructors ((T $eo_Term))
  :signature ($eo_Term) $eo_Term
  (
  ; TODO
  ; otherwise, a datatype declared in the signature
  (($eo_dt_constructors T) ($eo_dt_constructors_main T))
  )
)

; fwd-decl: $eo_dt_selectors_main
; note: This method returns the constructors for all user defined terms.
(program $eo_dt_selectors_main () :signature ($eo_Term) $eo_Term)

; program: $eo_dt_selectors
; implements: eo::dt_selectors
(program $eo_dt_selectors ((t $eo_Term))
  :signature ($eo_Term) $eo_Term
  (
  ; TODO
  ; otherwise, a datatype constructor declared in the signature
  (($eo_dt_selectors t) ($eo_dt_selectors_main t))
  )
)
