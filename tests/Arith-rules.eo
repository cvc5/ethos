; depends: arith_programs.plf

(include "Builtin-theory.eo")
(include "Arith-theory.eo")
(include "Utils.eo")


(program arith_rel_sum ((T Type) (U Type) (V Type) (W Type) (a V) (b W))
  :signature (T U V W) Bool
  (
    ((arith_rel_sum < < a b) (< a b))
    ((arith_rel_sum < = a b) (< a b))
    ((arith_rel_sum < <= a b) (< a b))
    ((arith_rel_sum <= < a b) (< a b))
    ((arith_rel_sum <= = a b) (<= a b))
    ((arith_rel_sum <= <= a b) (<= a b))
    ((arith_rel_sum = < a b) (< a b))
    ((arith_rel_sum = = a b) (<= a b))  ; could be =, but internal proof checker does <=
    ((arith_rel_sum = <= a b) (<= a b))
  )
)

(program arith_rel_inv ((T Type) (U Type) (V Type) (W Type) (a U) (b V))
  :signature (T U V) Bool
  (
    ((arith_rel_inv = a b) (= a b))
    ((arith_rel_inv < a b) (> a b))
    ((arith_rel_inv <= a b) (>= a b))
    ((arith_rel_inv > a b) (< a b))
    ((arith_rel_inv >= a b) (<= a b))
  )
)

(program arith_rel_neg ((T Type) (U Type) (V Type) (W Type) (a U) (b V))
  :signature (T U V) Bool
  (
    ((arith_rel_neg < a b) (>= a b))
    ((arith_rel_neg <= a b) (> a b))
    ((arith_rel_neg > a b) (<= a b))
    ((arith_rel_neg >= a b) (< a b))
  )
)

(program arith_rel_trichotomy ((T Type) (U Type) (V Type) (W Type) (a V) (b W))
  :signature (T U V W) Bool
  (
    ((arith_rel_trichotomy = < a b) (> a b))
    ((arith_rel_trichotomy = > a b) (< a b))
    ((arith_rel_trichotomy > = a b) (< a b))
    ((arith_rel_trichotomy < = a b) (> a b))
    ((arith_rel_trichotomy > < a b) (= a b))
    ((arith_rel_trichotomy < > a b) (= a b))
  )
)

;
(program arith_normalize_lit ((T Type) (U Type) (r (-> T U Bool)) (a T) (b U))
  :signature (Bool) Bool
  (
    ((arith_normalize_lit (not (not (r a b)))) (r a b))
    ((arith_normalize_lit (not (r a b)))       (arith_rel_neg r a b))
    ((arith_normalize_lit (r a b))             (r a b))
  )
)

(program mk_arith_sum_ub_step
  ((T Type) (U Type) (S Type) (V Type) (r1 (-> T U Bool)) (a1 T) (b1 U) (r2 (-> S V Bool)) (a2 S :list) (b2 V :list))
  :signature (Bool Bool) Bool
  (
    ((mk_arith_sum_ub_step (r1 a1 b1) (r2 a2 b2)) (arith_rel_sum r1 r2 (+ a1 a2) (+ b1 b2)))
  )
)

(program mk_arith_sum_ub ((T Type) (U Type) (r (-> T U Bool)) (a T) (b U) (tail Bool :list))
    :signature (Bool) Bool
    (
        ((mk_arith_sum_ub true)               (= 0 0))
        ((mk_arith_sum_ub (and (r a b) tail)) (mk_arith_sum_ub_step (r a b) (mk_arith_sum_ub tail)))
    )
)

(declare-rule arith_sum_ub ((F Bool))
  :premise-list F and
  :conclusion (mk_arith_sum_ub F)
)

; Computes the conclusion of the PfRule::ARITH_MULT_POS rule.
(program mk_arith_mult_pos ((T Type) (U Type) (S Type) (r (-> T U Bool)) (a T) (b U) (m S))
  :signature (S Bool) Bool
  (
    ((mk_arith_mult_pos m (r a b)) (r (* m a) (* m b)))
  )
)

(declare-rule arith_mult_pos ((T Type) (m T) (F Bool))
  :args (m F)
  :conclusion (=> (and (> m 0) F) (mk_arith_mult_pos m F))
)

; Computes the conclusion of the PfRule::ARITH_MULT_NEG rule.
(program mk_arith_mult_neg ((T Type) (U Type) (S Type) (r (-> T U Bool)) (a T) (b U) (m S))
  :signature (S Bool) Bool
  (
    ((mk_arith_mult_neg m (r a b)) (arith_rel_inv r (* m a) (* m b)))
  )
)

(declare-rule arith_mult_neg ((T Type) (m T) (F Bool))
  :args (m F)
  :conclusion (=> (and (< m 0) F) (mk_arith_mult_neg m F))
)


; Computes the conclusion of the PfRule::ARITH_TRICHOTOMY rule.
(program mk_arith_trichotomy ((T Type) (U Type) (S Type) (r1 (-> T U Bool)) (r2 (-> T U Bool)) (a T) (b U) (m S))
  :signature (Bool S) Bool
  (
    ((mk_arith_trichotomy (r1 a b) (r2 a b)) (arith_rel_trichotomy r1 r2 a b))
  )
)

(declare-rule arith_trichotomy ((F1 Bool) (F2 Bool))
  :premises (F1 F2)
  :conclusion (mk_arith_trichotomy (arith_normalize_lit (not F1)) (arith_normalize_lit (not F2)))
)


; Returns true if c is a rational between zero and one, inclusive
(program between_zero_and_one ((R Type) (c R))
  :signature (R) Bool
  (
    ((between_zero_and_one c) 
      (eo::ite (eo::is_neg c)
        false
        (eo::ite (eo::is_eq c 1)
          true
          (eo::is_neg (eo::add c (eo::neg 1.0))))))
  )
)

; Returns true if c is the greatest integer less than (integer or real) constant
; t. We compute this via conditions 0 <= c-t ^ (c-t)-1 <= 0.
(declare-rule int_tight_ub ((s Int) (t Real) (c Int))
  :premises ((< s t))
  :args (c)
  :requires (((between_zero_and_one (eo::add t (eo::neg c))) true))
  :conclusion (<= s c)
)

; Returns true if c2 is the least integer greater than c1. We compute this
; via conditions 0 <= c1-c2 ^ (c1-c2)-1 <= 0.
(declare-rule int_tight_lb ((s Int) (t Real) (c Int))
  :premises ((> s t))
  :args (c)
  :requires (((between_zero_and_one (eo::add c (eo::neg t))) true))
  :conclusion (>= s c)
)
