(declare-const Int Type)
(declare-const Real Type)

(declare-consts <numeral> Int)
(declare-consts <decimal> Real)

(declare-parameterized-const = ((T Type :implicit)) (-> T T Bool))

(program a.typeunion ()
    :signature (Type Type) Type
    (
      ((a.typeunion Int Int) Int)
      ((a.typeunion Int Real) Real)
      ((a.typeunion Real Int) Real)
      ((a.typeunion Real Real) Real)
    )
)

(declare-parameterized-const + ((T Type :implicit) (U Type :implicit)) (-> T U (a.typeunion T U)))
(declare-parameterized-const - ((T Type :implicit) (U Type :implicit)) (-> T U (a.typeunion T U)))
(declare-parameterized-const < ((T Type :implicit)) (-> T T Bool))
(declare-parameterized-const <= ((T Type :implicit)) (-> T T Bool))

(program run_evaluate ((T Type) (U Type) (S Type) (a T) (b U) (z S))
    :signature (S) S
    (
      ((run_evaluate (= a b))  (eo::is_eq (run_evaluate a) (run_evaluate b)))
      ((run_evaluate (< a b))  (eo::is_neg (run_evaluate (- a b))))
      ((run_evaluate (<= a b)) (eo::define ((x (run_evaluate (- a b)))) 
                                 (eo::or (eo::is_neg x) (eo::is_eq (eo::to_q x) 0/1))))
      ((run_evaluate (+ a b))  (eo::add (run_evaluate a) (run_evaluate b)))
      ((run_evaluate (- a b))  (eo::add (run_evaluate a) (eo::neg (run_evaluate b))))
      ((run_evaluate z)        z)
    )
)

(declare-rule eval
   ((T Type) (U Type) (a T) (b U))
   :premises ()
   :args (a b)
   :requires (((run_evaluate a) (run_evaluate b)))
   :conclusion (= a b)
)

